plugins {
    id "com.android.library"
    id "kotlin-android"
}

ext {
    // Auto generate version code based on version name
    libVersionCode = libVersion.split(/\./).collect { it.padLeft(2, "0") }.sum().toInteger()

    publishedGroupId = "com.maltaisn"
    libraryName = "recurrence-picker"
    artifact = "recurpicker"
    packagingType = "aar"

    libraryDescription = "Recurrence picker view and dialog for Android"

    siteUrl = "https://github.com/maltaisn/recurpickerlib"
    gitUrl = "https://github.com/maltaisn/recurpickerlib.git"

    developerId = "maltaisn"

    licenseName = "The Apache Software License, Version 2.0"
    licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode libVersionCode
        versionName libVersion
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    implementation "androidx.core:core-ktx:$ktxVersion"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.annotation:annotation:1.1.0"

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}
